extern TypeReference
extern UppercaseReference
extern ValueReference
extern Number
extern StringLiteral
extern BracedTokenStream
extern SyntaxTokenLiteral

rule Program {
    captures {
        modules = repeated: Module
    }
    persist
    soi
    optional repeated {
        modules = Module
    }
    eoi
    return modules
}

rule Module {
    captures {
        header = ModuleHeader
        body = ModuleBody
    }
    header = ModuleHeader
    persist
    body = ModuleBody
    keyword END
}

rule ModuleHeader {
    captures {
        name = TypeReference
        oid = optional: ModuleIdentifier
        encoding_reference = optional: UppercaseReference
        tag_default = optional: TagDefault
        extension_default = optional: ExtensionDefault
        exports = optional: Exports
        imports = optional: Imports
    }
    name = TypeReference
    persist
    oid = optional ModuleIdentifier
    keyword DEFINITIONS
    optional {
        encoding_reference = UppercaseReference
        keyword INSTRUCTIONS
    }
    tag_default = optional TagDefault
    extension_default = optional ExtensionDefault
    operator ::=
    keyword BEGIN
    exports = optional Exports
    imports = optional Imports
}

rule ModuleBody {
    captures {
        assignments = repeated: Assignment
    }
    repeated {
        assignments = Assignment
    }
    return assignments
}

variant ModuleIdentifier = DefinitiveOidWithIri | DefinitiveOid;

rule DefinitiveOidWithIri {
    captures {
        oid = DefinitiveOid
        iri = StringLiteral
    }
    oid = DefinitiveOid
    iri = StringLiteral
}

rule DefinitiveOid {
    captures {
        components = repeated: DefinitiveOidComponent
    }
    operator {
    persist
    repeated {
        components = DefinitiveOidComponent
    }
    operator }
    return components
}

variant DefinitiveOidComponent = NamedNumber | ValueReference | Number;

rule NamedNumber {
    captures {
        name = ValueReference
        num = IntegerValueReference
    }
    name = ValueReference
    operator (
    persist
    num = IntegerValueReference
    operator )
}

rule NamedNumberList {
    captures {
        nums = repeated: NamedNumber
    }
    operator {
    nums = NamedNumber
    persist
    optional repeated {
        operator ,
        nums = NamedNumber
    }
    operator }
    return nums
}

rule TagDefault {
    captures {
        kind = TagDefaultKind
    }
    kind = TagDefaultKind
    persist
    keyword TAGS
    return kind
}

variant TagDefaultKind = TagDefaultExplicit {
    keyword EXPLICIT
} | TagDefaultImplicit {
    keyword IMPLICIT
} | TagDefaultAutomatic {
    keyword AUTOMATIC
};

variant ExtensionDefault = ExtensibilityImplied {
    keyword EXTENSIBILITY
    keyword IMPLIED
};

rule Exports {
    captures {
        kind = optional: ExportsKind
    }
    keyword EXPORTS
    persist
    kind = optional ExportsKind
    operator ;
    return kind
}

variant ExportsKind = All {
    keyword ALL
} | SymbolList;

rule Imports {
    captures {
        symbols_from_module = repeated: SymbolsFromModule
    }
    keyword IMPORTS
    persist
    optional repeated {
        symbols_from_module = SymbolsFromModule
    }
    operator ;
    return symbols_from_module
}

rule SymbolsFromModule {
    captures {
        symbols = SymbolList
        module = GlobalModuleReference
        selection_option = optional: SelectionOption
    }
    symbols = SymbolList
    keyword FROM
    persist
    module = GlobalModuleReference
    optional {
        selection_option = SelectionOption
    }
}

rule SelectionOption {
    captures {
        kind = SelectionOptionKind
    }
    keyword WITH
    persist
    kind = SelectionOptionKind
    return kind
}

variant SelectionOptionKind = Successors {
    keyword SUCCESSORS
} | Descendants {
    keyword DESCENDANTS
};

variant AssignedIdentifier = DefinitiveOid | ValueIdentifier {
    captures {
        value = DefinedValue
    }
    value = DefinedValue
    // We need to ensure that there is not a comma after the identifier.
    // Take this example from 'RFC1354-MIB.asn':
    // IMPORTS
    //     Gauge, IpAddress
    //             FROM RFC1155-SMI
    //     mib-2, ip
    //             FROM RFC1213-MIB
    // The first module is erroneously parsed as { name = RFC1155-SMI, id = mib-2 },
    // and then the first token available to SymbolList is the comma after mib-2.
    // In order to ensure that the first module is parsed as { name = RFC1155-SMI, id = None },
    // we need to ensure there is not a comma after the `id`.
    not operator ,
    // We also need to make sure that the next token is not the keyword FROM.
    // Take this example from 'SIP-COMMON-MIB.asn':
    // IMPORTS
    // ...
    // SipTCTransportProtocol,
    // SipTCMethodName,
    // SipTCEntityRole,
    // SipTCOptionTagHeaders
    //       FROM SIP-TC-MIB             -- RFC 4780
    // applIndex
    //       FROM NETWORK-SERVICES-MIB   -- RFC 2788
    // The first module is erroneously parsed as { name = SIP-TC-MIB, id = applIndex },
    // and then the first token available to SymbolList is the FROM before NETWORK-SERVICES-MIB.
    // In order to ensure that the first module is parsed as { name = SIP-TC-MIB, id = None },
    // we need to ensure that there is not a FROM after the `id`.
    not keyword FROM
    return value
};

rule GlobalModuleReference {
    captures {
        name = TypeReference
        oid = optional: AssignedIdentifier
    }
    name = TypeReference
    optional {
        oid = AssignedIdentifier
    }
}

variant Oid = ObjectIdentifierValue | DefinedValue;

rule ObjectIdentifierValue {
    captures {
        components = repeated: ObjectIdentifierComponent
    }
    operator {
    components = ObjectIdentifierComponent
    persist
    optional repeated {
        components = ObjectIdentifierComponent
    }
    operator }
}

variant ObjectIdentifierComponent = NamedNumber | DefinedValue | ValueReference | Number;

rule DefinedValue {
    captures {
        external_module = optional: TypeReference
        value = ValueReference
    }
    optional {
        external_module = TypeReference
        operator .
    }
    value = ValueReference 
}


rule SymbolList {
    captures {
        symbols = repeated: Symbol
    }
    symbols = Symbol
    persist
    optional repeated {
        operator ,
        symbols = Symbol
    }
    return symbols
}

variant Symbol = ParameterizedReference | Reference;

rule ParameterizedReference {
    captures {
        reference = Reference
    }
    reference = Reference
    operator {
    persist
    operator }
    return reference
}

variant Reference = TypeReference | ValueReference;

variant Assignment = ObjectSetAssignment | TypeAssignment | ValueAssignment;

rule ParameterDeclList {
    captures {
        parameters = repeated: ParameterDecl
    }
    operator {
    persist
    parameters = ParameterDecl
    optional repeated {
        operator ,
        parameters = ParameterDecl
    }
    operator }
    return parameters
}

variant ParameterDecl = ObjectSetParameterDecl {
    captures {
        class_type = UppercaseReference
        name = TypeReference
    }
    class_type = UppercaseReference
    operator :
    name = TypeReference
} | ValueParameterDecl {
    captures {
        ty = Type
        name = ValueReference
    }
    ty = Type
    operator :
    name = ValueReference
} | TypeParameterDecl {
    captures {
        name = TypeReference
    }
    name = TypeReference
    return name
};

rule TypeAssignment {
    captures {
        name = TypeReference
        parameters = optional: ParameterDeclList
        subject = TypeAssignmentSubject
    }
    name = TypeReference
    optional {
        parameters = ParameterDeclList
    }
    operator ::=
    persist
    subject = TypeAssignmentSubject
}

variant TypeAssignmentSubject = TypeIdentifier {
    keyword TYPE-IDENTIFIER
} | AbstractSyntax {
    keyword ABSTRACT-SYNTAX
} | InformationObjectClass | Type;

rule Constraint {
    captures {
        constraint = SubtypeConstraint
    }
    operator (
    constraint = SubtypeConstraint
    persist
    operator )
    return constraint
}

rule SubtypeConstraint {
    captures {
        element_sets = repeated: SubtypeElementSet
        extensible = bool
    }
    element_sets = SubtypeElementSet
    optional repeated {
        operator ,
        operator ...
        extensible = true
        operator ,
        element_sets = SubtypeElementSet
    }
    optional {
        operator ,
        operator ...
        extensible = true
    }
}

rule SubtypeElementSet {
    captures {
        elements = repeated: SubtypeElement
    }
    elements = SubtypeElement
    persist
    optional repeated {
        operator |
        elements = SubtypeElement
    }
    return elements
}

variant SubtypeElement = UserDefinedConstraint | TableConstraint | ContentsConstraint | InnerTypeConstraints | SizeConstraint | ValueRangeConstraint {
    captures {
        lower = RangeLowerBound
        upper = RangeUpperBound
    }
    lower = RangeLowerBound
    operator ..
    persist
    upper = RangeUpperBound
} | SingleValueConstraint {
    captures {
        value = Value
    }
    value = Value
    return value
};

rule TableConstraint {
    captures {
        set_name = TypeReference
        field_ref = optional: ValueReference
    }
    operator {
    persist
    set_name = TypeReference
    operator }
    optional {
        operator {
        operator @
        field_ref = ValueReference
        operator }
    }
}

variant ContentsConstraint = EncodedBy {
    captures {
        ty = Type
        value = Value
    }
    keyword CONTAINING
    ty = Type
    keyword ENCODED
    persist
    keyword BY
    value = Value
} | Containing {
    captures {
        ty = Type
    }
    keyword CONTAINING
    ty = Type
    return ty
};

rule UserDefinedConstraint {
    keyword CONSTRAINED
    persist
    keyword BY
    operator {
    operator }
}

rule SizeConstraint {
    captures {
        constraint = Constraint
    }
    keyword SIZE
    persist
    constraint = Constraint
    return constraint
}

variant RangeLowerBound = Min {
    keyword MIN
} | GtValue {
    captures {
        value = Value
    }
    value = Value
    operator <
    return value
} | Value;

variant RangeUpperBound = Max {
    keyword MAX
} | LtValue {
    captures {
        value = Value
    }
    operator <
    persist
    value = Value
    return value
} | Value;

rule InnerTypeConstraints {
    captures {
        spec = TypeConstraintSpec
    }
    keyword WITH
    persist
    keyword COMPONENTS
    spec = TypeConstraintSpec
    return spec
}

variant TypeConstraintSpec = FullSpec {
    captures {
        constraints = TypeConstraints
    }
    operator {
    constraints = TypeConstraints
    persist
    operator }
    return constraints
} | PartialSpec {
    captures {
        constraints = TypeConstraints
    }
    operator {
    operator ...
    persist
    operator ,
    constraints = TypeConstraints
    operator }
    return constraints
};

rule TypeConstraints {
    captures {
        constraints = repeated: NamedConstraint
    }
    constraints = NamedConstraint
    persist
    optional repeated {
        operator ,
        constraints = NamedConstraint
    }
    return constraints
}

rule NamedConstraint {
    captures {
        name = ValueReference
        constraint = ComponentConstraint
    }
    name = ValueReference
    persist
    constraint = ComponentConstraint
}

variant ComponentConstraint = Constraint | PresenceConstraint | ValuedPresenceConstraint {
    captures {
        value = Constraint
        presence = PresenceConstraint
    }
    value = Constraint
    presence = PresenceConstraint
};

variant PresenceConstraint = PresencePresent {
    keyword PRESENT
} | PresenceAbsent {
    keyword ABSENT
} | PresenceOptional {
    keyword OPTIONAL
};

rule Boolean {
    keyword BOOLEAN
}

rule Integer {
    captures {
        named_values = optional: NamedNumberList
    }
    keyword INTEGER
    persist
    optional {
        named_values = NamedNumberList
    }
}

rule BitString {
    captures {
        named_bits = optional: NamedNumberList
    }
    keyword BIT
    persist
    keyword STRING
    optional {
        named_bits = NamedNumberList
    }
}

rule OctetString {
    keyword OCTET
    persist
    keyword STRING
}

rule Null {
    keyword NULL
}

rule ObjectIdentifier {
    keyword OBJECT
    persist
    keyword IDENTIFIER
}

rule ObjectDescriptor {
    keyword ObjectDescriptor
}

rule External {
    keyword EXTERNAL
}

rule Real {
    keyword REAL
}

rule EnumerationItem {
    captures {
        name = ValueReference
        num = optional: Value
    }
    name = ValueReference
    persist
    optional {
        operator (
        num = Value
        operator )
    }
}

rule Enumerated {
    captures {
        items = repeated: EnumerationItem
    }
    keyword ENUMERATED
    persist
    operator {
    items = EnumerationItem
    optional repeated {
        operator ,
        items = EnumerationItem
    }
    operator }
    return items
}

rule EmbeddedPDV {
    keyword EMBEDDED
    persist
    keyword PDV
}

rule UTF8String {
    keyword UTF8String
}

rule RelativeOid {
    keyword RELATIVE-OID
}

rule Time {
    keyword TIME
}

variant StructureKind = Sequence {
    keyword SEQUENCE
} | Set {
    keyword SET
};

rule Structure {
    captures {
        kind = StructureKind
        components = repeated: StructureComponent
        extensible = bool
    }
    kind = StructureKind
    operator {
    persist
    optional {
        components = StructureComponent
    }
    optional repeated {
        operator ,
        components = StructureComponent
    }
    optional {
        operator ,
        operator ...
        extensible = true
    }
    operator }
}

variant ConstraintOrSizeConstraint = Constraint | SizeConstraint;

rule StructureOf {
    captures {
        kind = StructureKind
        constraint = optional: ConstraintOrSizeConstraint
        ty = boxed Type
    }
    kind = StructureKind
    optional {
        constraint = ConstraintOrSizeConstraint
    }
    keyword OF
    persist
    ty = Type
}

rule StructureComponent {
    captures {
        name = ValueReference
        ty = Type
        default = optional: Value
        optional = bool
    }
    name = ValueReference
    ty = Type
    persist
    optional {
        keyword DEFAULT
        default = Value
    }
    optional {
        keyword OPTIONAL
        optional = true
    }
}

rule ObjectClassFieldType {
    captures {
        class_type = UppercaseReference
        field = FieldReference
    }
    class_type = UppercaseReference
    operator .
    persist
    field = FieldReference
}

rule Choice {
    captures {
        alternatives = repeated: ChoiceAlternative
    }
    keyword CHOICE
    persist
    operator {
    alternatives = ChoiceAlternative
    optional repeated {
        operator ,
        alternatives = ChoiceAlternative
    }
    operator }
    return alternatives
}

rule ChoiceAlternative {
    captures {
        name = ValueReference
        ty = Type
    }
    name = ValueReference
    persist
    ty = Type
}

rule NumericString {
    keyword NumericString
}

rule PrintableString {
    keyword PrintableString
}

rule TeletexString {
    keyword TeletexString
}

rule VideotexString {
    keyword VideotexString
}

rule IA5String {
    keyword IA5String
}

rule UTCTime {
    keyword UTCTime
}

rule GeneralizedTime {
    keyword GeneralizedTime
}

rule GraphicString {
    keyword GraphicString
}

rule VisibleString {
    keyword VisibleString
}

rule GeneralString {
    keyword GeneralString
}

rule UniversalString {
    keyword UniversalString
}

rule CharacterString {
    keyword CHARACTER
    keyword STRING
}

rule BMPString {
    keyword BMPString
}

rule Date {
    keyword DATE
}

rule TimeOfDay {
    keyword TIME-OF-DAY
}

rule DateTime {
    keyword DATE-TIME
}

rule Duration {
    keyword DURATION
}

variant BuiltinType = Boolean | Integer | BitString | OctetString | Null | ObjectIdentifier | ObjectDescriptor | External |
    Real | Enumerated | EmbeddedPDV | UTF8String | RelativeOid | Time | Structure | Choice | NumericString | PrintableString |
    TeletexString | VideotexString | IA5String | UTCTime |  GeneralizedTime | GraphicString | VisibleString | GeneralString |
    UniversalString | CharacterString | BMPString | Date | TimeOfDay | DateTime | Duration;

variant Class = Universal {
    keyword UNIVERSAL
} | Application {
    keyword APPLICATION
} | Private {
    keyword PRIVATE
};

rule Tag {
    captures {
        encoding_reference = optional: UppercaseReference
        class = optional: Class
        class_number = Number
    }
    operator [
    persist
    optional {
        encoding_reference = UppercaseReference
        operator :
    }
    optional {
        class = Class
    }
    class_number = Number
    operator ]
}

variant TagKind = TagKindImplicit {
    keyword IMPLICIT
} | TagKindExplicit {
    keyword EXPLICIT
};

rule TaggedType {
    captures {
        tag = Tag
        kind = optional: TagKind
        ty = boxed ConstrainedType
    }
    tag = Tag
    persist
    optional {
        kind = TagKind
    }
    ty = ConstrainedType
}

variant ConstrainedType = Suffixed {
    captures {
        ty = UntaggedType
        constraint = optional: Constraint
    }
    ty = UntaggedType
    optional {
        constraint = Constraint
    }
} | TypeWithConstraint {
    captures {
        ty = StructureOf
    }
    ty = StructureOf
    return ty
};

rule ParameterizedTypeReference {
    captures {
        name = TypeReference
        parameters = ParameterList
    }
    name = TypeReference
    parameters = ParameterList
}

rule ParameterList {
    captures {
        parameters = repeated: Parameter
    }
    operator {
    persist
    parameters = Parameter
    optional repeated {
        operator ,
        parameters = Parameter
    }
    operator }
    return parameters
}

variant Parameter = ObjectSetParameter {
    captures {
        object_set = TypeReference
    }
    operator {
    persist
    object_set = TypeReference
    operator }
    return object_set
} | TypeParameter {
    captures {
        ty = Type
    }
    ty = Type
    return ty
} | ValueParameter {
    captures {
        value = Value
    }
    value = Value
    return value
};

variant UntaggedType = BuiltinType | ObjectClassFieldType | ParameterizedTypeReference | TypeReference;

variant Type = TaggedType | ConstrainedType;

rule ValueAssignment {
    captures {
        name = ValueReference
        ty = Type
        value = Value
    }
    name = ValueReference
    ty = Type
    persist
    operator ::=
    value = Value
}

rule ObjectSetAssignment {
    captures {
        name = TypeReference
        ty = UppercaseReference
        subject = ObjectSetAssignmentSubject
    }
    name = TypeReference
    ty = UppercaseReference
    operator ::=
    subject = ObjectSetAssignmentSubject
}

variant ObjectSetAssignmentSubject = EmptyExtensible {
    operator {
    operator ...
    persist
    operator }
} | ObjectSet {
    captures {
        elements = repeated: ObjectSetElement
        extensible = bool
    }
    operator {
    elements = ObjectSetElement
    persist
    optional repeated {
        operator |
        elements = ObjectSetElement
    }
    optional {
        operator ,
        operator ...
        extensible = true
    }
    operator }
};

variant ObjectSetElement = ValueReference | BracedTokenStream;

rule NamedBitStringValue {
    captures {
        bits = repeated: ValueReference
    }
    operator {
    optional {
        bits = ValueReference
    }
    optional repeated {
        operator ,
        bits = ValueReference
    }
    operator }
    return bits
}

rule StructureValue {
    captures {
        components = repeated: StructureValueComponent
    }
    operator {
    components = StructureValueComponent
    optional repeated {
        operator ,
        components = StructureValueComponent
    }
    operator }
}

rule StructureValueComponent {
    captures {
        name = ValueReference
        value = Value
    }
    name = ValueReference
    value = Value
}

rule StructureOfValue {
    captures {
        elements = repeated: Value
    }
    operator {
    optional {
        elements = Value
    }
    optional repeated {
        operator ,
        elements = Value
    }
    operator }
    return elements
}

rule ChoiceValue {
    captures {
        alternative = ValueReference
        value = boxed Value
    }
    alternative = ValueReference
    operator :
    persist
    value = Value
}

rule OpenTypeValue {
    captures {
        open_type = OpenTypeValueTypeReference
        value = boxed Value
    }
    open_type = OpenTypeValueTypeReference
    operator :
    persist
    value = Value
}

variant OpenTypeValueTypeReference = ObjectValueFieldReference | Type;

rule Negative {
    operator -
}

rule IntegerValue {
    captures {
        sign = optional: Negative
        value = Number
    }
    optional {
        sign = Negative
    }
    value = Number
}

rule DecimalValue {
    captures {
        sign = optional: Negative
        whole = Number
        fraction = Number
    }
    optional {
        sign = Negative
    }
    whole = Number
    operator .
    persist
    fraction = Number
}

variant BooleanValue = True {
    keyword TRUE
} | False {
    keyword FALSE
};

variant SpecialRealValue = PlusInfinity {
    keyword PLUS-INFINITY
} | MinusInfinity {
    keyword MINUS-INFINITY
} | NotANumber {
    keyword NOT-A-NUMBER
};

rule ContainingValue {
    captures {
        value = boxed Value
    }
    keyword CONTAINING
    persist
    value = Value
    return value
}

variant BuiltinValue = ContainingValue | OpenTypeValue | ObjectValueFieldReference | Null | BooleanValue | SpecialRealValue | StringLiteral | DecimalValue | IntegerValue | ChoiceValue | BracedTokenStream;

variant Value = BuiltinValue | ValueReference;
variant IntegerValueReference = IntegerValue | ValueReference;

rule InformationObjectClass {
    captures {
        fields = repeated: ObjectClassField
        syntax = SyntaxList
    }
    keyword CLASS
    persist
    operator {
    fields = ObjectClassField
    optional repeated {
        operator ,
        fields = ObjectClassField
    }
    operator }
    keyword WITH
    keyword SYNTAX
    syntax = SyntaxList
}

variant ObjectClassField = ObjectField | ObjectSetField | TypeField | ValueField;

rule TypeField {
    captures {
        name = TypeFieldReference
        optional = bool
    }
    name = TypeFieldReference
    persist
    optional {
        keyword OPTIONAL
        optional = true
    }
}

rule ObjectField {
    captures {
        name = ValueFieldReference
        class = UppercaseReference
        optional = bool
    }
    name = ValueFieldReference
    class = UppercaseReference
    optional {
        keyword OPTIONAL
        optional = true
    }   
}

rule ObjectSetField {
    captures {
        name = TypeFieldReference
        class = UppercaseReference
        optional = bool
    }
    name = TypeFieldReference
    class = UppercaseReference
    optional {
        keyword OPTIONAL
        optional = true
    }   
}

rule ValueField {
    captures {
        name = ValueFieldReference
        ty = Type
        default = optional: Value
        optional = bool
        unique = bool
    }
    name = ValueFieldReference
    persist
    ty = Type
    optional {
        keyword DEFAULT
        default = Value
    }
    optional {
        keyword OPTIONAL
        optional = true
    }
    optional {
        keyword UNIQUE
        unique = true
    }
}

rule TypeFieldReference {
    captures {
        name = TypeReference
    }
    operator &
    name = TypeReference
    return name
}

rule ValueFieldReference {
    captures {
        name = ValueReference
    }
    operator &
    name = ValueReference
    return name
}

variant FieldReference = TypeFieldReference | ValueFieldReference;

rule SyntaxList {
    captures {
        elements = repeated: SyntaxListElement
    }
    operator {
    persist
    repeated {
        elements = SyntaxListElement
    }
    operator }
}

variant SyntaxListElement = OptionalSyntaxElement {
    captures {
        items = repeated: SyntaxItem
    }
    operator [
    persist
    repeated {
        items = SyntaxItem
    }
    operator ]
    return items
} | RequiredSyntaxElement {
    captures {
        item = SyntaxItem
    }
    item = SyntaxItem
    return item
};

variant SyntaxItem = FieldReference | SyntaxTokenLiteral;

rule ObjectValueFieldReference {
    captures {
        object_name = ValueReference
        field_ref = FieldReference
    }
    object_name = ValueReference
    operator .
    persist
    field_ref = FieldReference
}
